\\gp /mnt/c/GitProjects/oeis/data/notes/lucas_snfs_poly.txt

\\ https://www.mersenneforum.org/showthread.php?t=23895

default(realprecision, 1000);

assert(predicate,err)=if(!cond,error(err));

\\ Credit to Charles Greathouse for the dickmanrho, rhoest, and deBruijnXi implementation

eps() = 10^(-5); \\ precision

debruijnxi(x)={
    my(left, right, m);
    if (x < 1, error ("debruijnxi: Can't find a xi given x < 1."));
    if (x > 1, left = log(x), left = eps());
    right = 1.35 * log(x) + 1; \\ Heuristic

    \\Bisection
    while (right - left > left * eps(),
        m = (left + right) / 2;
        if (exp(m) - 1 > x * m, right = m, left = m)
    );
    return((left + right) / 2);
};
addhelp(debruijnxi, "debruijnxi(x): Helper function for rhoest.  Finds a xi such that e^xi - 1 = x * xi.");

rhoest(x)={
    my(xi = debruijnxi(x));
    \\exp(Euler) / sqrt(2 * Pi * x) * exp(1 - exp(xi) + intnum(s = eps(), xi, (exp(s) - 1) / s))
    return(exp(-eint1(-xi) - x * xi) / sqrt(2 * Pi * x) / xi)
};
addhelp(rhoest, "de Bruijn's asymptotic approximation for rho(x), rewritten as in van de Lune and Wattel 1969.  Curiously, their paper shows values for this estimate that differ from those calculated by this function, often as soon as the second decimal place -- but as the difference is in the direction of the true value, I have not looked further into this.");


rhoTable = [1, 3.068528194e-1, 4.860838829e-2, 4.910925648e-3, 3.547247005e-4, 1.964969635e-5, 8.745669953e-7, 3.232069304e-8, 1.016248283e-9, 2.770171838e-11, 6.644809070e-13, 1.419713165e-14, 2.729189030e-16, 4.760639989e-18, 7.589908004e-20];
dickmanrho(x)={
    my(x=real(x));
    local(left, right, scale);
    if (x <= 2, return (1 - log(max(x, 1))));
    if (x <= 3, return(
        1 - (1 - log(x - 1))*log(x) + real(dilog(1 - x)) + Pi^2 / 12
    ));

    \\ Asymptotic estimate (scaled for continuity)
    if (x > #rhoTable,
        scale = rhoTable[#rhoTable] / rhoest(#rhoTable);

        \\ Let the scale factor dwindle away, since the estimate is (presumably)
        \\ better in the long run than any scaled version of it.  The exponent
        \\ of 0.25 has been chosen to give the best results for 10 < x < 100
        \\ with a table size of 10.
        scale = (scale - 1) * (#rhoTable / x)^.25 + 1;
        return (precision(rhoest(x) * scale, 9))
    );

    \\ Scaling factors: the factor by which the true value of rho differs from
    \\ the estimates at the endpoints.
    left = rhoTable[floor(x)] / rhoest(floor(x));
    right = rhoTable[ceil(x)] / rhoest(ceil(x));

    \\ Linear interpolation on the scale factors.
    scale = left + (right - left) * (x - floor(x));

    \\ Return a result based on the scale factor and the asymptotic formula.
    return(precision(rhoest(x) * scale, 9));
};
addhelp(dickmanrho, "Estimates the value of the Dickman rho function. For x <= 3 the exact values are used, up to rounding; up to \"#rhoTable\" the value is interpolated using known values and rhoest; after \"#rhoTable\" rhoest is used, along with a correction factor based on the last value in rhoTable.");

\\ The following 4 functions were adapted from CADO-NFS Murphy E score calculation

special_valuation(f, p, disc) = {
    d = poldegree(f);
    pvaluation_disc = 0;
    if(disc % p == 0,
        pvaluation_disc++;
        t = disc \ p;
        if(t % p == 0, pvaluation_disc++);
    p_divides_lc = polcoef(f, d);
    if(pvaluation_disc == 0,
        e = #polrootsmod(f, p);
        if(p_divides_lc,
            assert(polcoef(f, d-1) % p);
            e++);
        return((p * e) / (p * p - 1)),
    pvaluation_disc == 1,
        e = #polrootsmod(f, p));
        if(p_divides_lc, e++;);
        return((p * e - 1) / (p * p - 1)),
    \\ default
        v = special_val0(f, p) * p;
        if(p_divides_lc,
           g = subst(polrecip(f),'x,p*'x);
           v += special_val0(g, p);
           );
        v /= p + 1.0;
        return(v);
        );
}

special_val0(f, p) = {
    c = content(f);
    i = poldegree(f);
    v = 0;
    while(c % p == 0, v++; c = c \ p);
    g = f\(p^v);
    h = subst(g, 'x, p*'x);
    assert(d > 0);
    roots = liftall(polrootsmod(g,p));
    nroots = #roots;
    assert(nroots <= d);
    r0 = 0;
    foreach(roots, r,
        c = subst(g, 'x, r);
        if(c % p,
            v += 1.0 / (p - 1),
        \\default
            assert(r >= r0);
            h = subst(h, 'x, 'x + (r - r0)\p);
            r0 = r;
            v += special_val0(h, p) \ p));
    return(v);
}

get_alpha(poly, B) = {
    \\ for F linear, we have q_p = 1 for all p, thus alpha(F) = sum(prime p <= B, log(p)/(p^2-1)) ~ 0.569959993064325
    if(poldegree(poly) == 1, return(0.569959993064325));
    disc = poldisc(poly);
    e = special_valuation(poly, 2, disc);
    alpha = (1.0 - e) * log(2.0);
    forprime(p=3, B, e = special_valuation(poly, p, disc); alpha += (1.0 / (p - 1) - e) * log(p));
    return(alpha);
}

polyscore(pols, skew) = {
    \\unfortunately the murphy scores seem off for the moment, resort to coefficient sums
    return(vecsum(abs(Vec(pols[1]))));
    if(skew == 0, skew = calculate_skew(pols));
    \\ defaults taken from cado-nfs muprhy score calculation
    bound_f = 1e7;
    bound_g = 5e6;
    area = 1e16;
    alpha_bound = 2000;
    k = 1000;

    x = sqrt(area * skew);
    y = sqrt(area / skew);
    f = pols[1];
    g = pols[2];
    alpha_f = get_alpha(f, alpha_bound);
    alpha_g = get_alpha(g, alpha_bound);
    one_over_log_bound_f = 1.0 / log(bound_f);
    one_over_log_bound_g = 1.0 / log(bound_g);
    e = 0;
    for(i=0, k-1,
        ti = Pi / k * (i + 0.5);
        xi = x * cos(ti);
        yi = y * sin(ti);
        vf = subst(f, 'x, xi / yi) * yi^poldegree(f);
        vg = subst(f, 'x, xi / yi) * yi^poldegree(g);
        vf = log(abs(vf)) + alpha_f;
        vg = log(abs(vg)) + alpha_g;
        vf *= one_over_log_bound_f;
        vg *= one_over_log_bound_g;
        e += dickmanrho(vf) * dickmanrho(vg));
    return(real(e / k));
}

\\ lucas2.gp
lucas2(k,P,Q,     n,s,j,Ql,Qh,Uh,Vl,Vh) = {
        n = floor(log(k)/log(2)) + 1;
        s = 0;
        while( bittest(k,s) == 0, s = s + 1 );
        Uh = 1; Vl = 2; Vh = P;
        Ql = 1; Qh = 1;
        forstep( j = n-1, s+1, -1,
                Ql = Ql * Qh;
                if( bittest(k,j),
                        Qh = Ql * Q;
                        Uh = Uh * Vh;
                        Vl = Vh * Vl - P * Ql;
                        Vh = Vh * Vh - 2 * Qh,

                        Uh = Uh * Vl - Ql;
                        Vh = Vh * Vl - P * Ql;
                        Vl = Vl * Vl - 2 * Ql;
                        Qh = Ql;
                );
        );
        Ql = Ql * Qh; Qh = Ql * Q;
        Uh = Uh * Vl - Ql;
        Vl = Vh * Vl - P * Ql;
        Ql = Ql * Qh;
        for( j = 1, s,
                Uh = Uh * Vl;
                Vl = Vl * Vl - 2 * Ql;
                Ql = Ql * Ql;
        );
        return([Uh,Vl]);
}
U(p,q,n)=lucas2(n,p,q)[1];
V(p,q,n)=lucas2(n,p,q)[2];


sympoly(d) = {
   pret=Vec(0,d+1);
   for(i=0,d,pret[i+1]='y^i*'x^(d-i));
   return(pret);
}

plindep(N,d,x,y,comment) = {
    vectors = subst(subst(concat(N, sympoly(d)), 'x, x), 'y, y);
    
    result = lindep(vectors);
    
    if (result[1] == 0,
       \\ Trivial relation found (coeff of N is 0). Try to find next best using qflll.
       K_scale = 10^100;
       W = matid(#vectors);
       W[1,1] = 0; \\ Allow large coeff for N
       M_rel = matconcat([K_scale * Mat(vectors); W]);
       U = qflll(M_rel);
       Basis = M_rel * U;
       
       found = 0;
       for(j=1, #vectors,
           if (Basis[1, j] == 0 && Basis[2, j] != 0,
               result = Basis[2..#vectors+1, j];
               found = 1;
               break;
           );
       );
       if (!found, result = Basis[2..#vectors+1, 1]); \\ Fallback
    );

    polygcd = content(result[2..#result]);
    apol = Polrev(result[2..#result]\polygcd);
    rpol = (-y+x*'x)\content([x,y]);
    return([apol, rpol, comment, N]);
}

snfs_difficulty(pols) = {
    my(x=-polcoef(pols[2],0)/polcoef(pols[2],1),
       val=polcoef(pols[2],1)^poldegree(pols[1])*eval(pols[1]));
    if(val==0, return(99999999999));
    return(log(abs(val))/log(10));
}

gnfs_difficulty(pols) = {
    return(snfs_difficulty(pols)*.56+30);
}

calculate_skew(poly) = {
    d = poldegree(poly[1]);
    return((abs(polcoef(poly[1],0))/abs(polcoef(poly[1],d)))^(1/d));
}

printpoly(pols) = {
    N = pols[4];
    if(poldegree(pols[1]) > 3,
        snfs_diff = snfs_difficulty(pols);
        gnfs_diff = snfs_diff*.56+30;
        comment = pols[3];
        skew = calculate_skew(pols);
        if(comment, printf("# %s\n", comment));
        printf("# Score: %.06e\n", polyscore(pols, skew));
        printf("# SNFS difficulty: %.01f\n# GNFS equivalent: %.01f\n", snfs_diff, gnfs_diff);
        printf("# 4/13 pretest: t%.02f\n", gnfs_diff*4/13);
        printf("#  1/3 pretest: t%.02f\n", gnfs_diff/3);
        printf("type: snfs\nn: %d\nskew: %.02f\n", N, skew);
        for (i = 0, d, if(polcoef(pols[1],i) != 0, printf("c%d: %d\n", i, polcoef(pols[1],i))));
        printf("Y0: %d\nY1: %d\n\n", polcoef(pols[2],0), polcoef(pols[2],1));
    );
}

invert(pols) = [Polrev(Vec(pols[1])), Polrev(Vec(pols[2])), pols[3], pols[4]];

subxk(pols, k) = {
    \\ reduce by gcd if not done already
    apol = pols[1];
    rpol = pols[2];
    apolvec = Vec(apol);
    rpolvec = Vec(rpol);
    apolvec = apolvec\content(apolvec);
    rpolvec = rpolvec\content(rpolvec);
    \\ substitute k
    apolkvec = Vec(subst(Pol(apolvec,'x),'x,'x+k));
    rpolkvec = Vec(subst(Pol(rpolvec,'x),'x,'x+k));
    \\reduce again if necessary
    apolkvec = apolkvec\content(apolkvec);
    rpolkvec = rpolkvec\content(rpolkvec);
    apolk = Pol(apolkvec, 'x);
    rpolk = Pol(rpolkvec, 'x);
    return([apolk, rpolk, pols[3], pols[4]]);
}

jiggle(pols) = {
    invpols = invert(pols);
    retpolys = List([pols, invpols]);
    for(i=1,5,
        retpolys = concat(retpolys, List([subxk(pols, i), subxk(invpols, i), subxk(pols, -i), subxk(invpols, -i)])));
    return(retpolys);
}

\\public api, implemented first for V case, will have to adjust for U case
polygen(isu, p,q,k,composite=0, printpolys=0) = {
    seqf = if(isu, U, V);
    parentN = seqf(p,q,k);
    has3 = k % 3 == 0;
    has5 = k % 5 == 0;
    has7 = k % 7 == 0;
    has11 = k % 11 == 0;
    has13 = k % 13 == 0;
    has9 = k % 9 == 0;
    paramsvec = List();
    if((k-2)%4==0 && !isu && p==2 && q==-1,
            n = (k-2)\4;
            n_h = 2*n+1;
            foreach([-1, 1], c,
                N = 2*U(p,q,n_h)+c;
                if (composite == 0 || N % composite == 0,
                    foreach([4,6,8], d,
                        foreach([[U(p,q,n_h\d+1), U(p,q,n_h\d)],
                                 [U(p,q,n_h\d+1), U(p,q,n_h\d-1)]], yvec,
                            paramsvec = concat(paramsvec, [N, d, yvec[1], yvec[2], "hcpell diff of squares case"]))))));
    if(has9,
            o = 9;
            y1 = 1;
            y0 = -seqf(p,q,k\o); \\works in V(k\9); \\ cpells
            N = seqf(p,q,k)\seqf(p,q,k\3);
            d = 6; \\ cpells have a d6 poly for n%9==0
            paramsvec = concat(paramsvec, [N, d, y1, y0, "9 divisibility case"]));
    if(has3 && has5,
            o = 15;
            N = seqf(p,q,k)*seqf(p,q,k\o)\(seqf(p,q,k\3)*seqf(p,q,k\5));
            d = 4;
            y1 = 1;
            y0 = seqf(p,q,2*k\o);
            paramsvec = concat(paramsvec, [N, d, y1, y0, "15 divisibility case"]);
            d = 8;
            y1 = 1;
            y0 = seqf(p,q,2*k\o); \\ TODO double check this works, probably needs a different constant
            \\print("WARNING: 3 * 5 divisibility case could maybe be improved");
            paramsvec = concat(paramsvec, [N, d, y1, y0, "15 divisibility case"]));
    if(has3 && has7,
            o = 21;
            N = seqf(p,q,k)*seqf(p,q,k\o)\(seqf(p,q,k\3)*seqf(p,q,k\7));
            d = 6;
            y1 = 1;
            y0 = seqf(p,q,2*k\o);
            paramsvec = concat(paramsvec, [N, d, y1, y0, "21 divisibility case"]));
    if(has3 || has5 || has7 || has11 || has13, \\ other simple divisibilities
            ovec = [];
            if(has3, ovec = concat(ovec, 3));
            if(has5, ovec = concat(ovec, 5));
            if(has7, ovec = concat(ovec, 7));
            if(has11, ovec = concat(ovec, 11));
            if(has13, ovec = concat(ovec, 13));
            for( j = 1, #ovec, o = ovec[j];
                               N = seqf(p,q,k)\seqf(p,q,k\o);
                               dp = eulerphi(o);
                               dvec = if(dp==2, [4,6,8], dp>8, [dp\2], dp>6, [dp, dp\2], [dp]);
                               for(i = 1, #dvec, d = dvec[i];
                                   paramsvec = concat(paramsvec, [N, d, U(p,q,(o-1)*k\o\d+1), U(p,q,(o-1)*k\o\d), strprintf("%d divisibility case", o)]);
                                   paramsvec = concat(paramsvec, [N, d, U(p,q,(o-1)*k\o\d+1), U(p,q,(o-1)*k\o\d-1), strprintf("%d divisibility case", o)]);
                                   paramsvec = concat(paramsvec, [N, d, V(p,q,(o-1)*k\o\d+1), V(p,q,(o-1)*k\o\d), strprintf("%d divisibility case", o)]);
                                   paramsvec = concat(paramsvec, [N, d, V(p,q,(o-1)*k\o\d+1), V(p,q,(o-1)*k\o\d-1), strprintf("%d divisibility case", o)]);
                                   paramsvec = concat(paramsvec, [N, d, 1, U(p,q,k\o), strprintf("%d divisibility case", o)]);
                                   paramsvec = concat(paramsvec, [N, d, 1, V(p,q,k\o), strprintf("%d divisibility case", o)]))));

    if(1, \\ prime or composite with no small factors case
            if(composite > 0,
                N = composite,
                N = seqf(p,q,k);
                \\ find small factors:
                factors = factor(N,10^5); \\ TODO might need adjusting
                div = 1;
                for(i = 1, #factors[,1]-1, div=div * factors[i,1]^factors[i,2]);
                N = seqf(p,q,k)\div;
            );
            foreach([4,5,6,7,8], d,
                foreach([U,V], base_func,
                    my(m=k\d);
                    foreach([m, m+1], i,
                        foreach([
			         [base_func(p,q,i), base_func(p,q,i-1)], \\ for some reason U works better always \\ TODO confirm this
                                 [base_func(p,q,i), base_func(p,q,i-2)],
                                 [base_func(p,q,i), base_func(p,q,i+2)]
                                ], yvec,
                                paramsvec = concat(paramsvec, [N, d, yvec[1], yvec[2], "default case"]))))));
    paramsvec = Vecrev(paramsvec);
    result_polys = List();
    for (i = 1, #paramsvec, params = paramsvec[i];
                            N = params[1];
                            d = params[2];
                            y1 = params[3];
                            y0 = params[4];
                            comment = params[5];
                            poly = plindep(N, d, y1, y0, comment);
                            if (vecsum(abs(Vec(poly[1]))) < 10^15,
                                if(composite, poly[4] = composite);
                                result_polys = concat(result_polys, jiggle(poly))
                            ););
    for(i=1, #result_polys, result_polys[i] = concat(concat(result_polys[i], snfs_difficulty(result_polys[i])), polyscore(result_polys[i])));
    result_polys = vecsort(result_polys, polysortcmp, 8);
    if(printpolys, apply(printpoly, Vecrev(result_polys)));
    return(result_polys);
}


polygenu(p, q, k, composite=0, printpolys=0) = polygen(1, p, q, k, composite, printpolys);
polygenv(p, q, k, composite=0, printpolys=0) = polygen(0, p, q, k, composite, printpolys);

polyfind(isu, p, q, k, composite=0, numpolys=50) = {
    result_polys = polygen(isu, p, q, k, composite);
    final_polys = Vecrev(result_polys[1..numpolys]);
    apply(printpoly, final_polys);
    return();
}

polyfindu(p, q, k, composite=0, numpolys=10) = polyfind(1, p, q, k, composite, numpolys);
polyfindv(p, q, k, composite=0, numpolys=10) = polyfind(0, p, q, k, composite, numpolys);

mindifficulty(isu, p, q, k, composite=0) = {
    return(vecmin(Vec(apply(gnfs_difficulty, polygen(isu, p, q, k, composite)))));
}

mindifficultyu(p, q, k, composite=0) = mindifficulty(1, p, q, k, composite);
mindifficultyv(p, q, k, composite=0) = mindifficulty(0, p, q, k, composite);

snfsbetter(isu, p, q, k, composite) = {
    assert(composite <= 0, "remaining composite must be given to determine if GNFS or SNFS is better");
    return(mindifficulty(isu, p, q, k, composite) < #digits(composite));
}

snfsbetteru(p, q, k, composite) = snfsbetter(1, p, q, k, composite);
snfsbetterv(p, q, k, composite) = snfsbetter(0, p, q, k, composite);

polysortcmp(x, y) = {
    coef_diff = vecsum(abs(Vec(x[1]))) - vecsum(abs(Vec(y[1])));
    \\ if coeff diff is > 10^20, sort by that (get rid of the fails)
    if(abs(coef_diff) > 10^20, coef_diff);
    xdegree = poldegree(x[1]);
    ydegree = poldegree(y[1]);
    xsnfs = x[5];
    ysnfs = y[5];
    \\ if they have a difference of 20 or more SNFS difficulty, sort based on that
    snfs_diff_diff = xsnfs - ysnfs;
    if(abs(snfs_diff_diff) >= 20, return(snfs_diff_diff));
    xdegree_distance = sign(degree_distance(xdegree, xsnfs));
    ydegree_distance = sign(degree_distance(ydegree, ysnfs));
    distance_difference = xdegree_distance - ydegree_distance;
    \\ if one is "in the pocket", and the other isn't, put the in the pocket one first.
    if(abs(distance_difference), return(distance_difference));
    xscore = x[6];
    yscore = y[6];
    \\ last compare polyscore;
    return(xscore - yscore);
}

\\ distance in decimal digits away from the "optimal degree zone" for SNFS only, i.e.
\\4: <100
\\5: 100-200
\\6: 200-260?
\\7: 260?
\\8: 260?
degree_distance(deg, difficulty) = {
    return(
        if( deg == 4, zone_distance(difficulty, 0, 100),
            deg == 5, zone_distance(difficulty, 100, 200),
            deg == 6, zone_distance(difficulty, 200, 260),
            zone_distance(difficulty, 260, 1000)));
}

\\ helper for above function, just outputs 0 if in the bounds or distance to the closest bound if not
\\ assumes positive inputs
zone_distance(value, low_bound, high_bound) = {
    return(
        if(value < low_bound, low_bound - value,
           value > high_bound, value - high_bound,
           0));
}

\\Regarding poly selection for indices divisible by 3: for CPell(3n)/CPell(n), you can use the usual lindep method, but as the number is similar in size to CPell(2n) you should pick Y0 and Y1 accordingly. Only even degrees will be possible.

\\ for generalized lucas numbers with composite index:
\\Originally Posted by charybdis
\\The rules for these numbers are essentially the same as those for Cunninghams:
\\
\\Index divisible by 3 -> poly of any even degree
\\Index divisible by 5 -> quartic (or octic, but that's only useful for difficulties above 260)
\\Index divisible by 7 -> sextic
\\Index divisible by 11 -> quintic
\\Index divisible by 13 -> sextic
\\Index divisible by 3 and 5 -> quartic (or octic for difficulties above 260)
\\Index divisible by 3 and 7 -> sextic
\\
\\There is the slightly easier sextic for an index divisible by 9 which I don't think has a direct Cunningham parallel; 2LM and 6LM are similar, but then there is no sextic for indices divisible by 3 but not 9.
\\
\\All other combinations of factors do not yield viable SNFS polynomials. For example, divisibility by 17 gives an octic but this will sieve slower than the sextic you get without dividing out the factor. Divisibility by 3 and 11 gives a degree 10 polynomial which is hopeless.

\\TODO https://www.mersenneforum.org/showthread.php?p=453018&highlight=lucas#post453018


\\k = 520
\\p = 2
\\q = -1
\\f = factor(k,13);
\\o = 13
\\N = V(p,q,k)\V(p,q,k\o)
\\d = 6
\\x = V(p,q,d*k\o\d+1)
\\y = V(p,q,d*k\o\d)
\\result = plindep(N,d,x,y)
\\printpoly(N, result)
\\jiggle(N, result)
\\x = 1
\\y = V(p,q,k\o)
\\result = plindep(N,d,x,y)
\\printpoly(N, result)
\\jiggle(N, result)
\\x = U(p,q,(o-1)*k\o\d+1)
\\y = U(p,q,(o-1)*k\o\d)
\\result = plindep(N,d,x,y)
\\printpoly(N, result)
\\jiggle(N, result)